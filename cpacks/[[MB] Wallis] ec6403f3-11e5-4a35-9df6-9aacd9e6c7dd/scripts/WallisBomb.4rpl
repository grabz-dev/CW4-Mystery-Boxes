# --WallisBomb-- 8/4/2020 10:36:50 AM

$$MAX_SPEED:0.8
$$ACCELERATION:0.01
$$GRAVITY:0.012
$$DAMAGE_DIST:7
$$DAMAGE_DEPTH:10
$$Lifetime:1800

GetUnitPosition(self) ->position

<-Lifetime 1 sub ->Lifetime
<-Lifetime 0 lt if
	<-position.x <-position.z -500 AddCreeper
	DamageUnit(self 2.0)
endif 


<-position <-lastPosition - ->delta

<-delta <-force + <-G - ->newDelta

if (RandFloat 0.001 lt)
	<-newDelta V3(RandFloat 0.5 - 0.01 *, 0.1, RandFloat 0.5 - 0.01 *) + ->newDelta
endif

if (magnitude(<-newDelta) <-MAX_SPEED gt)
	<-MAX_SPEED normalize(<-newDelta) * ->newDelta
endif

<-position ->lastPosition
<-position <-newDelta + ->position

BounceTerrainCollision(<-position, <-lastPosition, false, true, 0.5, 1) ->nlp ->np
if (<-np.w) 
	<-np ->position
	<-nlp ->lastPosition
endif

BounceEdgeCollision(<-position, <-lastPosition, 1) ->nlp ->np
if (<-np.w) 
	<-np ->position
	<-nlp ->lastPosition
endif

#Calculate a new force based on
Normalize(<-position <-lastPosition -) <-ACCELERATION * ->force

SetUnitPosition(self <-position)

GetExactCreeperHeight(<-position.x <-position.z false true) ->height ->surfaceNormal ->hasCreeper ->hasAC
if (<-hasCreeper <-height gt (<-position.y 1 -) &&)
	#DestroyUnit(self false false true)
	@DamageCreeper
	PlaySoundAtPosition("Explosion_12" 0.6 GetUnitPosition(self))
	DamageUnit(self 0.4)
endif

:DamageCreeper
	GetUnitCell(self) ->cellZ ->cellX
	DamageCreeper(<-cellX <-cellZ 9999 <-DAMAGE_DIST <-DAMAGE_DEPTH false)
	CreateEffect("explosion4" GetUnitPosition(self) V3(0.7 0.7 0.7))

:Destroyed
	if (<-base neq0)
		GetScript1Var(<-base, "wallisCount") ->patrolCount
		SetScript1Var(<-base, "wallisCount", <-patrolCount 1 -)
	endif	

:Once
	GetMapSize ->mapHeight ->mapWidth
	GetUnitPosition(self) ->lastPosition
	<-fireVel ->force
	V3(0, <-GRAVITY, 0) ->G
	CreateTrail(self "Sphere" 0 0.5)