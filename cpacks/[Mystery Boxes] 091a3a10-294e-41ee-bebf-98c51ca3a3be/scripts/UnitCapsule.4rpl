# --UnitCapsule-- 9/14/2021 2:04:57 PM

Self GetUnitConstructing Self GetUnitStunCount 0 gt or if
	return
endif
Self GetUnitEnabled not if
	return
endif

Self GetUnitMoveCell pop ->moving
<-moving -1 neq if
	1 ->moving
endif

<-moving <-lastMoving neq <-moving -1 eq and if
	Self 3 GetUnitUIState 1 eq if
		@Deploy
	endif
endif

<-moving ->lastMoving

:awake
	once
		Self "light" <-Color SetObjColor
		Self 1 <-Name SetUnitUIText
		-1 ->moving
		-1 ->lastMoving
		
		-?Rim if
			Self "rim" <-Rim SetObjColor
		ENDIF
	endonce
:once
	@awake
:_uicallback
	#Things to do when the DEPLOY button is clicked
	<-_DATA 2 eq if
		@Deploy
	endif
:SpawnFloatingText
	->SFT_tData #text, lifetime, speed, size, color
	
	<-*MB_tCustomGUIDs{"Floating Text"} Self GetUnitPosition <-SFT_tData CreateUnit pop
:Deploy
	#Make sure the capsule is landed
	Self GetUnitMoveCell pop -1 neq if
		"text" "Must land before deploying" "lifetime" 30 "color" 1 0.58 0.58 1 V4 3 TableN @SpawnFloatingText
	else
		#Make sure the capsule is not too close to a recycler
		<-*MB_tCustomGUIDs{"Recycler"} Self GetUnitPosition 6 1 1 0 0 0 0 GetUnits ->lUnits
		<-lUnits GetListCount 0 gt if
			"text" "Too close to Recycler to deploy" "lifetime" 30 "color" 1 0.58 0.58 1 V4 3 TableN @SpawnFloatingText
		else
			Self GetUnitCell ->iZ ->iX
			
			<-UnitType "ultrac" eq
			<-UnitType "emitter" eq or
			<-UnitType "resourcered" eq or
			<-UnitType "resourceblue" eq or
			<-UnitType "greenarmother" eq or if
				"MB_UnitCapsuleSpecialPlaced" "type" <-UnitType "x" <-iX "z" <-iZ "opts" <-UnitOpts 4 TableN SendMVerseMsg
			endif
		
			<-UnitType "solar_farm" eq if
				@BuildSingleplayerSolarFarm
			else <-UnitType "u_wall" eq if
				@BuildSingleplayerWall
			else
				<-UnitType <-iX <-iZ <-UnitOpts CreateUnitOnTerrain ->iUnit
				#Do not construct the unit if we're playing Mverse and this is a shared unit
				GetMversePlayerNum 0 gte <-*MB_tbMverseSharedUnits{<-UnitType} and if
				
				else
					<-iUnit 9999 ConstructUnit
				endif
			endif endif
			
			
			Self 1 1 1 DestroyUnit
		endif
	endif
:BuildSingleplayerSolarFarm
	6 0 do
		"collectorpanel5" <-iX <-iZ NULL CreateUnitOnTerrain pop
	loop
	"collectorpanel3" <-iX 1 sub <-iZ 2 sub NULL CreateUnitOnTerrain pop
:BuildSingleplayerWall
	<-iZ 4 add ->BSW_iZTop
	<-iZ 4 sub ->BSW_iZBottom
	<-iX 4 add ->BSW_iXRight
	<-iX 4 sub ->BSW_iXLeft
	
	<-BSW_iZTop 1 add <-BSW_iZBottom do
		<-BSW_iXRight 1 add <-BSW_iXLeft do
			J <-BSW_iZTop eq J <-BSW_iZBottom eq or I <-BSW_iXRight eq or I <-BSW_iXLeft eq or if
				"wall" I J NULL CreateUnitOnTerrain pop
			endif
		loop
	loop
