# --ACBomb-- 4/20/2022 5:17:35 PM

$ACPerPacket:11
$ACPerPackerWithERN:13

Self GetUnitConstructing Self GetUnitStunCount 0 gt or if
	return
endif

Self GetUnitAmmo Self GetUnitMaxAmmo gte if
	Self 2 GetUnitUIState 1 eq if
		@Detonate
	endif
endif

Self GetUnitERNState ->curERNState
<-curERNState <-lastERNState neq if
	<-curERNState if
		Self 5 SetUnitPacketRequestRate
	else
		Self 7 SetUnitPacketRequestRate
	endif
	@UpdateUI
	<-curERNState ->lastERNState
endif

:Awake
	@UpdateUI
:Once
	@Awake
:_UICALLBACK
	Self GetUnitConstructing Self GetUnitStunCount 0 gt or if
		return
	endif

	<-_DATA 3 eq if
		Self GetUnitAmmo Self GetUnitMaxAmmo gte if
			@Detonate
		endif
	endif
:Detonate
	Self GetUnitAmmo Self GetUnitERNState if <-ACPerPackerWithERN else <-ACPerPacket endif mul 9.0 div ->payload
	Self GetUnitPosition ToCell ->z ->x
	<-z 2 add <-z 1 sub do
		<-x 2 add <-x 1 sub do
			I J GetCreeper <-payload sub -2000 lte if
				I J -2000 TRUE SetCreeper
			else
				I J <-payload neg AddCreeper
			endif
		loop
	loop
	Self 0 SetUnitAmmo
	"Sci Fi Explosion 04" 8 PlaySound
	"radial" Self GetUnitPosition 2 2 2 V3 CreateEffect
:UpdateUI
	Self 6 "Deposits " Self GetUnitERNState if <-ACPerPackerWithERN else <-ACPerPacket endif " AC" concat3 SetUnitUIText
	Self 7 "per ammo packet" SetUnitUIText
