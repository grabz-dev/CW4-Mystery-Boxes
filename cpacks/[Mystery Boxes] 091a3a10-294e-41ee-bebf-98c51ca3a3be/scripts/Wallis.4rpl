# --Wallis-- 8/4/2020 10:33:55 AM

$$MAX_WALLIS:10
$$AMMO_USE:10
$$FIRE_TIME:60

Self GetUnitConstructing Self GetUnitStunCount 0 gt or if
	return
endif

if (<-mapInd neq0)
	if (GetGameUpdateCount 600 eq)
		DestroyUnit(<-mapInd true false true)
		0 ->mapInd
	endif	
endif

if (<-coolDown lte0 GetUnitArmed(self) && GetUnitEnabled(self) &&)
	if (<-wallisCount lt(<-MAX_WALLIS))
		GetUnitAmmo(self) ->ammo
		if (<-ammo <-AMMO_USE gte)
			@Fire
			SetUnitAmmo(self <-ammo <-AMMO_USE -)
			<-FIRE_TIME ->coolDown
		endif
	endif
endif	
if (<-cooldown gt0)
	<-coolDown 1 - ->coolDown
endif	

:Fire
	self ->initParams{"base"}
	V3(@RandFloatSeeded 0.5 - 0.1 *, 0.3, @RandFloatSeeded 0.5 - 0.1 *) ->initParams{"fireVel"}
	1 10000000 @RandIntSeeded ->initParams{"Seed"}
	CreateUnit(<-*MB_tCustomGUIDs{"Wallis Bomb"}, GetUnitPosition(self) V3(0 4.5 0) +, <-initParams) ->shotUnit
	<-wallisCount 1 + ->wallisCount
	@CreateMist
	PlaySoundAtPosition("Sci Fi Explosion 10" 0.6 GetUnitPosition(self))
	
	
:CreateMist
	do(30 0)
		CreateMist(GetUnitPosition(self) V3(0 4.5 0) +, <-mistData)
	loop
	
:Once
	-?Seed not if
		1 10000000 RandInt ->Seed
	endif

	SetUnitPacketRequestRate(self 4)
	CreateTable ->initParams
	Table("color" V3(1 0.8 0.8) 
		 "color2" V3(0.2 0 0)
		 "birthtime" 40 
		 "fadetime" 20 
		 "direction" V3(0 5 0) 
		 "startradius" 0.5
		 "endradius" 3	) ->mistData
# i1 i2 - i3
:RandIntSeeded
	->RIS_iMaxExcl
	->RIS_iMinIncl
	@RandFloatSeeded ->RIS_iRand
	
	<-RIS_iMaxExcl <-RIS_iMinIncl sub <-RIS_iRand mul floor <-RIS_iMinIncl add
# - f1
:RandFloatSeeded
	<-Seed sin 10000.0 mul ->RFS_fVal
	<-Seed 1 add ->Seed
	
	<-RFS_fVal <-RFS_fVal floor sub
	